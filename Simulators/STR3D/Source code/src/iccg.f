      SUBROUTINE ICCG(A,RLOW,B,X,WK,NEQ,NEQEXT,NCGSPC,IDSK,IDCG,INDOF
     *               ,NDF,MXITCG,CGTOL,ITO,IDCOMP,IMPC)
C
      USE MPI_PARAM
C
      IMPLICIT REAL*8(A-H,O-Z)
C
      DIMENSION A(NCGSPC),B(NEQEXT),X(NEQEXT),WK(NEQEXT,6)
     *         ,IDSK(NEQEXT+1),IDCG(NCGSPC),RLOW(NCGSPC),INDOF(NDF,*)
C
      MAXITR=MXITCG
      IF(MAXITR.EQ.0) THEN
        CALL CG_MPI_ALLREDUCE_I(NEQ,MAXITR,1,0)
        MAXITR=MAXITR/5
        IF(MAXITR.LT.50) MAXITR=50
      ENDIF
C
      DO 300 I=1,NEQ
        IF( A(IDSK(I)) .LE. 0.D0 ) THEN
          WRITE(ITO,*) 'PIVOT .LE. 0. , PROGRAM STOPPED !'
          CALL ERRSTP(90,ITO)
        ENDIF
  300 CONTINUE
C
      CALL SCALSY(NEQ,NEQEXT,WK(1,4),A,B,IDSK,IDCG,INDOF,NDF,IMPC)
      CALL DTPRDCT(BNRM,B,B,NEQ)
      IF(BNRM.EQ.0.D0) THEN
        CALL CLEAR1(X,NEQ)
        RETURN
      ENDIF
      TOL=CGTOL*CGTOL*BNRM
C
      IF(IDCOMP.EQ.0) THEN
        CALL CG3ILU(WK(1,3),A,NEQ,IDSK,IDCG,NCGSPC,ITO)
        CALL SHIFT1(RLOW,A,NCGSPC)
      ELSE
        CALL ICHSKY(WK(1,3),A,RLOW,NEQ,IDSK,IDCG,ITO)
      ENDIF
C
      CALL SHIFT1(WK(1,1),B,NEQ)
      CALL SHIFT1(WK(1,2),WK(1,1),NEQ)
      CALL CG3FWB(WK(1,3),RLOW,WK(1,2),WK(1,6),NEQ,IDSK,IDCG)
      CALL DTPRDCT(BETA1,WK(1,2),WK(1,1),NEQ)
      CALL CLEAR1(X,NEQ)
C
      DO 100 ITER=1,MAXITR
        ALP1=BETA1
        CALL CG3ML2(WK(1,5),A,WK(1,2),NEQEXT,IDSK,IDCG,NCGSPC,INDOF,NDF
     &             ,IMPC,ITO)
        CALL DTPRDCT(ALP2,WK(1,2),WK(1,5),NEQ)
        ALP=ALP1/ALP2
        CALL RMULT4(X,X,ALP,WK(1,2),NEQ)
        CALL RMULT4(WK(1,1),WK(1,1),-ALP,WK(1,5),NEQ)
C
        CALL DTPRDCT(RES,WK(1,1),WK(1,1),NEQ)
C
        IF( RES < TOL .OR. ( ITER == MAXITR .AND. RES < TOL*1.D3 ) )
     &    GOTO 200
C
        BETA2=BETA1
        CALL SHIFT1(WK(1,5),WK(1,1),NEQ)
        CALL CG3FWB(WK(1,3),RLOW,WK(1,5),WK(1,6),NEQ,IDSK,IDCG)
        CALL DTPRDCT(BETA1,WK(1,5),WK(1,1),NEQ)
        BETA=BETA1/BETA2
        CALL RMULT4(WK(1,2),WK(1,5),BETA,WK(1,2),NEQ)
  100 CONTINUE
C
      WRITE(ITO,*) 'NOT CONVERGED BY CG METHOD.'
      CALL ERRSTP(21,ITO)
C
  200 CALL VECML3(X,WK(1,4),X,NEQ)
C
      IF( NPROCS > 1 ) THEN
        CALL COMM_CG(X,INDOF,NDF)
        IF( IMPC > 0 ) CALL COMM_CGX(X,INDOF,NDF)
      ENDIF
C
      END        
